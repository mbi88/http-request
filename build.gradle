import groovy.io.FileType

plugins {
    id 'ru.vyarus.quality' version '4.6.0'
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'jacoco'

def suitesDir = 'src/test/resources/suites/'

repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
    jcenter()
}

dependencies {
    implementation group: 'org.testng', name: 'testng', version: '7.4.0'
    implementation group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.29'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.6'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
}

test {
    useTestNG() {
        // Add test suites
        def suitesList = []
        def dir = new File(suitesDir)
        dir.eachFileRecurse(FileType.FILES) { file -> suitesList << file }
        suitesList.each { options.suites(it.path) }

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = false
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

quality {
    checkstyle = true
    pmd = true
    codenarc = true
    spotbugs = true
}

check.dependsOn jacocoTestReport
